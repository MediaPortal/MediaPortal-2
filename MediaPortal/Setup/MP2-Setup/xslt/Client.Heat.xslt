<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0"
                xmlns="http://wixtoolset.org/schemas/v4/wxs"
                xmlns:wix="http://wixtoolset.org/schemas/v4/wxs"
                xmlns:xsl="http://www.w3.org/1999/XSL/Transform">

  <xsl:key name="MP2-Client.exe"
           match="//wix:Component[wix:File/@Source = '$(var.MediaPortal.Client.TargetDir)\MP2-Client.exe']"
           use="@Id"/>

  <xsl:key name="MP2-Client_x64.exe"
           match="//wix:Component[wix:File/@Source = '$(var.MediaPortal.Client.TargetDir)\MP2-Client (x64).exe']"
           use="@Id"/>

  <!-- Get the Tv client and provider plugin directories so they can be excluded, they are included in their own features. -->
  <xsl:key name="SlimTv.Client"
           match="//wix:Directory[@Name = 'SlimTv.Client']//wix:Component"
           use="@Id"/>
  <xsl:key name="SlimTv.NativeProvider"
           match="//wix:Directory[@Name = 'SlimTv.NativeProvider']//wix:Component"
           use="@Id"/>
  <xsl:key name="SlimTv.TvMosaicProvider"
           match="//wix:Directory[@Name = 'SlimTv.TvMosaicProvider']//wix:Component"
           use="@Id"/>

  <!-- Copy all nodes from source to target and apply templates. -->
  <xsl:template match="node()|@*">
    <xsl:copy>
      <xsl:apply-templates select="node()|@*"/>
    </xsl:copy>
  </xsl:template>

  <!-- Add comment to the beginning of the file and continue applying. -->
  <xsl:template match="wix:Wix">
    <Wix>
      <xsl:comment>This file was generated by the XSLT stylesheet 'Client.Heat.xslt'</xsl:comment>
      <xsl:apply-templates/>
    </Wix>
  </xsl:template>

  <!--
  Remove application executable from harvested file. It is explicitly defined
  in 'Features\*.wxs to reuse it for shortcut creation or service registration.
  -->
  <xsl:template match="wix:Component[@Id = key('MP2-Client.exe', @Id)/@Id]"/>
  <xsl:template match="wix:ComponentRef[@Id = key('MP2-Client.exe', @Id)/@Id]"/>
  <xsl:template match="wix:Component[@Id = key('MP2-Client_x64.exe', @Id)/@Id]"/>
  <xsl:template match="wix:ComponentRef[@Id = key('MP2-Client_x64.exe', @Id)/@Id]"/>

  <!-- Explicitly set DirectoryID to allow referencing to it. -->
  <xsl:template match="wix:DirectoryRef[@Id = 'INSTALLDIR_CLIENT']/wix:Directory[@Name = 'Plugins']">
    <Directory Id="CLIENT_PLUGINS_FOLDER" Name="Plugins">
      <xsl:apply-templates/>
    </Directory>
  </xsl:template>

  <!-- Rewrite definition for MP2-ClientLauncher (fixed GUID, add to autostart...) -->
  <xsl:key name="MP2-ClientLauncher.exe"
           match="//wix:Component[wix:File/@Source = '$(var.MediaPortal.Client.TargetDir)\Tools\MP2-ClientLauncher\MP2-ClientLauncher.exe']"
           use="@Id"/>

  <xsl:template match="wix:Component[@Id = key('MP2-ClientLauncher.exe', @Id)/@Id]">

    <Component Id="ClientLauncher.exe" Guid="51C55F34-A2BC-45FB-8949-7104556C7AD9">
      <File Id="ClientLauncher.exe" KeyPath="yes" Source="$(var.MediaPortal.Client.TargetDir)\Tools\MP2-ClientLauncher\MP2-ClientLauncher.exe" />
    </Component>

    <!-- Auto-start via Registry -->
    <Component Id="ClientLauncher.Registry.AutoStart" Guid="B602BA6F-90F7-4450-8E8C-C9EDFC6BEE6D">
      <RegistryValue Root="HKCU"
                     Key="Software\Microsoft\Windows\Currentversion\Run"
                     Name="MP2 ClientLauncher"
                     Value='"[#ClientLauncher.exe]"'
                     KeyPath="yes"
                     Type="expandable"/>
    </Component>

  </xsl:template>

  <xsl:template match="wix:ComponentRef[@Id = key('MP2-ClientLauncher.exe', @Id)/@Id]">
    <ComponentRef Id="ClientLauncher.exe" />
    <ComponentRef Id="ClientLauncher.Registry.AutoStart" />
  </xsl:template>

  <!-- Exclude Tv server plugin directories and components, they are included in their own features. -->
  <xsl:template match="wix:Directory[@Name = 'SlimTv.Client']"/>
  <xsl:template match="wix:ComponentRef[@Id = key('SlimTv.Client', @Id)/@Id]"/>
  <xsl:template match="wix:Directory[@Name = 'SlimTv.NativeProvider']"/>
  <xsl:template match="wix:ComponentRef[@Id = key('SlimTv.NativeProvider', @Id)/@Id]"/>
  <xsl:template match="wix:Directory[@Name = 'SlimTv.TvMosaicProvider']"/>
  <xsl:template match="wix:ComponentRef[@Id = key('SlimTv.TvMosaicProvider', @Id)/@Id]"/>
  
</xsl:stylesheet>
